apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'webstart'

version = '0.1'


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

mainClassName = 'ch.scbirs.trainingmanager.MainWrapper'

jar {
    manifest {
        attributes 'Main-Class': project.mainClassName,
                'Implementation-Title': 'TrainingManager',
                'Implementation-Version': version,
                'provider': 'gradle'
    }
}

sourceSets {
    mwrapper {
        java {
            srcDir 'src/main/java'
            include 'ch/scbirs/trainingmanager/MainWrapper.java'
        }
        compileClasspath += main.output
    }
    main {
        java {
            exclude 'ch/scbirs/trainingmanager/MainWrapper.java'
        }
    }
}

compileMwrapperJava {
    sourceCompatibility = "1.5"
    targetCompatibility = "1.5"
}

jar {
    from sourceSets.mwrapper.output
}

webstart {
    title = "TrainingManager"
    codebase = "https://dl.dropboxusercontent.com/u/49598155/jnlp/"
    jnlpHref = "TrainingManager.jnlp"
    vendor = "scbirs.ch"
    homepage = "scbirs.ch"
    appDescription = "Training Management Tool"
}

if (hasProperty('sign_keystore')) {
    jar.doLast {
        new File('build/signed').mkdirs();
        ant.signjar(destDir: 'build/signed',
                jar: archivePath,
                alias: "ch.scbirs",
                storetype: "JKS",
                keystore: sign_keystore,
                storepass: sign_password,
                preservelastmodified: "true")
    }
}