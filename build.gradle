apply plugin: 'java'
apply plugin: 'application'

version = '0.1'
mainClassName = 'ch.scbirs.trainingmanager.MainWrapper'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar {
    manifest {
        attributes 'Main-Class': project.mainClassName,
                'Implementation-Title': 'TrainingManager',
                'Implementation-Version': version,
                'provider': 'gradle'
    }
    from sourceSets.mwrapper.output
}

sourceSets {
    mwrapper {
        java {
            srcDir 'src/main/java'
            include 'ch/scbirs/trainingmanager/MainWrapper.java'
        }
        compileClasspath += main.output
    }
    main {
        java {
            exclude 'ch/scbirs/trainingmanager/MainWrapper.java'
        }
    }
}

compileMwrapperJava {
    sourceCompatibility = JavaVersion.VERSION_1_5
    targetCompatibility = JavaVersion.VERSION_1_5
}


processResources {
    doLast {
        def versionfile = new File(destinationDir, 'version.txt')
        versionfile.write(project.version)
    }
}

if (hasProperty('sign_keystore')) {
    jar.doLast {
        new File('build/signed').mkdirs();
        ant.signjar(destDir: 'build/signed',
                jar: archivePath,
                alias: "ch.scbirs",
                storetype: "JKS",
                keystore: sign_keystore,
                storepass: sign_password,
                preservelastmodified: "true")
    }
}